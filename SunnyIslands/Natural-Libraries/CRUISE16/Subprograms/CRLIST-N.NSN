* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram CRLIST-N.
/**
/** :author sagadmin
/* TODO Enter your code here
DEFINE DATA 
PARAMETER USING NCCOMM-P /* Comm Parameters
PARAMETER USING NCCRUL-P /* Cruise Paramaters
*
LOCAL USING NCDATA-L
*
LOCAL
1 C-RECCNT (P10)
1 T-PRICE (N6.2)
1 REDEFINE T-PRICE
  2 T-PRICE-A (A8)
*
1 V-SERVICENAME (A20) CONST<'Cruise list'>
1 V-WORKREC (A80)
1 V-WORKFIELD (A16) 
1 LOCAL-AVAIL (N1)
*
END-DEFINE
*
* ------------------------------------------------------------------
ON ERROR
  REDUCE ARRAY P-CRUISE-DATA TO  0
  INCLUDE ERRLOG-I 'V-SERVICENAME'
  ESCAPE ROUTINE
END-ERROR
* ------------------------------------------------------------------
MOVE ALL '-' TO V-WORKFIELD
*
MOVE P-LANG TO MSG-GROUP-PARA.MSG-LANG
* ------------------------------------------------------------------
*

IF #STUDENT  THEN

  MOVE 9999 TO MSG-GROUP-PARA.MSG-NR

ELSE

*
MOVE 0 TO P-RESPONSE.P-RSPCODE
*

READ NCCRUISE DESCENDING BY START-DATE

  COMPUTE LOCAL-AVAIL = VAL(NCCRUISE.CRUISE-STATUS)
  
  IF LOCAL-AVAIL EQ 0      /* cruise is fully booked
   ESCAPE TOP
  END-IF
        
  IF P-STARTHARBOR NE ' ' AND NCCRUISE.START-HARBOR NE P-STARTHARBOR
    ESCAPE TOP
  END-IF
  IF P-DESTHARBOR NE ' ' AND NCCRUISE.DESTINATION-HARBOR NE P-DESTHARBOR
    ESCAPE TOP
  END-IF

ADD 1 TO C-RECCNT
EXPAND ARRAY P-CRUISE-DATA TO (1:C-RECCNT)
*
MOVE BY NAME NCCRUISE TO P-CRUISE-DATA(C-RECCNT)


MOVE EDITED  NCCRUISE.START-DATE (EM=9999'-'99'-'99) TO P-CRUISE-DATA.START-DATE(C-RECCNT)
MOVE EDITED  NCCRUISE.END-DATE   (EM=9999'-'99'-'99) TO P-CRUISE-DATA.END-DATE(C-RECCNT)
MOVE EDITED  NCCRUISE.PRICE-1W (EM=ZZZZZZZZZ9.99) TO  P-CRUISE-DATA.PRICE-1W(C-RECCNT)
MOVE EDITED  NCCRUISE.PRICE-2W (EM=ZZZZZZZZZ9.99) TO  P-CRUISE-DATA.PRICE-2W(C-RECCNT)
MOVE EDITED  NCCRUISE.PRICE-3W (EM=ZZZZZZZZZ9.99) TO  P-CRUISE-DATA.PRICE-3W(C-RECCNT)
*
*
  F1.
  FIND NCYACHT NCYACHT.YACHT-ID = NCCRUISE.ID-YACHT
    COMPRESS NCYACHT.YACHT-NAME INTO P-CRUISE-DATA.YACHT-NAME(C-RECCNT)
    ESCAPE BOTTOM (F1.)
  END-FIND

END-READ

*
DECIDE FOR FIRST CONDITION
  WHEN C-RECCNT = 0
    MOVE 9857 TO MSG-GROUP-PARA.MSG-NR
  WHEN NONE
    MOVE 9807 TO MSG-GROUP-PARA.MSG-NR
END-DECIDE
END-IF
* ------------------------------------------------------------------
CALLNAT 'CAMSG-N' MSG-GROUP-PARA
COMPRESS MSG-GROUP-PARA.MSG-TYPE '-' MSG-GROUP-PARA.MSG-TEXT 
  INTO P-RESPONSE.P-RSPTXT
MOVE MSG-GROUP-PARA.MSG-NR TO P-RESPONSE.P-RSPCODE  
* ------------------------------------------------------------------
*
END
